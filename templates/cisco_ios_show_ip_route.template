Value Filldown Protocol (C|S|R|B|D|O|i|L)
Value Filldown Type (\w{0,2})
Value Required,Filldown Network ([0-9]{1,3}.[0-9]{1,3}.[0-9]{1,3}.[0-9]{1,3})
Value Filldown Mask (\/\d{1,2})
Value Distance (\d+)
Value Metric (\d+)
Value NexthopIP ([0-9]{1,3}.[0-9]{1,3}.[0-9]{1,3}.[0-9]{1,3})
Value NexthopIf ([\w\/]*)
Value Uptime ([0-9]{2}:[0-9]{2}:[0-9]{2})

Start
  ^Gateway.* -> Routes

Routes
  #
  # Match regular routes with all data in same line
  ^${Protocol}(\s|\*)${Type}\s+${Network}${Mask}\s\[${Distance}\/${Metric}\]\svia\s${NexthopIP}(,\s${Uptime}){0,1}(,\s${NexthopIf}){0,1} -> Record
  #
  # Clear all non Filldown variables when line started with network that is variably subnetted
  ^\s+[0-9]{1,3}.[0-9]{1,3}.[0-9]{1,3}.[0-9]{1,3}${Mask} -> Clear
  ^${Protocol}(\s|\*)${Type}\s+${Network}\s\[${Distance}\/${Metric}\]\svia\s${NexthopIP}(,\s${Uptime}){0,1}(,\s${NexthopIf}){0,1} -> Record
  #
  # Match load-balanced routes
  ^\s+\[${Distance}\/${Metric}\]\svia\s${NexthopIP} -> Record
  #
  # Match directly connected routes
  ^${Protocol}\s${Type}\s+${Network}\sis\sdirectly\sconnected,\s${NexthopIf} -> Record
  ^${Protocol}(\*){0,1}\s${Type}\s+${Network}${Mask}\sis\sdirectly\sconnected,\s${NexthopIf} -> Record
  #
  # Clear all variables on empty lines
  ^\s* -> Clearall

EOF