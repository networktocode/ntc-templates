Value Filldown VENDOR_ID_BWD_COMPAT (\S+)
Value Filldown CALL_STATION_ID_CASE (\S+)
Value Filldown ACCT_CALL_STATION_ID_TYPE (.+)
Value Filldown AUTH_CALL_STATION_ID_TYPE (.+)
Value Filldown EXT_SRC_PORTS_SUPPORT (\S+)
Value Filldown AGGRESSIVE_FAILOVER (\S+)
Value Filldown KEYWRAP (\S+)
Value Filldown FALLBACK_TEST_MODE (\S+)
Value Filldown FALLBACK_TEST_USERNAME (.+)
Value Filldown FALLBACK_TEST_INTERVAL (\d+)
Value Filldown MAC_DELIM_AUTHC_MSG (\S+)
Value Filldown MAC_DELIM_ACCT_MSG (\S+)
Value Filldown AUTHC_FRAMED_MTU (\d+)
Value Required INDEX (\d+)
Value AUTHC_SRV_ADDR (\S+)
Value ACCT_SRV_ADDR (\S+)
Value TYPE (\S+)
Value SRV_PORT (\d+)
Value SRV_STATE (\S+)
Value SRV_TIMEOUT (\d+)
Value SRV_MGMT_TIMEOUT (\d+)
Value RFC3576 (\S+)
Value IPSEC_STATE (\S+)
Value IPSEC_PROFILE (\S+)
Value IPSEC_REGION_STR (\S+)

Start
  ^\s*Vendor Id Backward Compatibility\s*\.+\s+${VENDOR_ID_BWD_COMPAT}\s*$$
  ^\s*Call Station I[dD] Case\s*\.+\s+${CALL_STATION_ID_CASE}\s*$$
  ^\s*Accounting Call Station I[dD] Type\s*\.+\s+${ACCT_CALL_STATION_ID_TYPE}\s*$$
  ^\s*Auth Call Station I[dD] Type\s*\.+\s+${AUTH_CALL_STATION_ID_TYPE}\s*$$
  ^\s*Extended Source Ports Support\s*\.+\s+${EXT_SRC_PORTS_SUPPORT}\s*$$
  ^\s*Aggressive Failover\s*\.+\s+${AGGRESSIVE_FAILOVER}\s*$$
  ^\s*Keywrap\s*\.+\s+${KEYWRAP}\s*$$
  ^\s*Fallback Test:\s*$$
  ^\s+Test Mode\s*\.+\s+${FALLBACK_TEST_MODE}\s*$$
  ^\s+Probe User Name\s*\.+\s+${FALLBACK_TEST_USERNAME}\s*$$
  ^\s+Interval \(in seconds\)\.+\s+${FALLBACK_TEST_INTERVAL}\s*$$
  ^\s*MAC Delimiter for Authentication Messages\s*\.+\s+${MAC_DELIM_AUTHC_MSG}\s*$$
  ^\s*MAC Delimiter for Accounting Messages\s*\.+\s+${MAC_DELIM_ACCT_MSG}\s*$$
  ^\s*RADIUS Authentication Framed-MTU\s*\.+\s+${AUTHC_FRAMED_MTU}\s[bB]ytes\s*$$
  ^\s*Authentication Servers\s*$$ -> AuthC_Servers
  ^\s*$$
  ^. -> Error

AuthC_Servers
  ^\s*Idx\s+Type\s+Server Address\s+Port\s+State\s+Tout\s+MgmtTout\s+RFC3576\s+IPSec - state\/Profile Name\/RadiusRegionString\s*$$
  ^-+
  ^\s*${INDEX}\s+\*\s+${TYPE}\s+${AUTHC_SRV_ADDR}\s+${SRV_PORT}\s+${SRV_STATE}\s+${SRV_TIMEOUT}\s+${SRV_MGMT_TIMEOUT}\s+${RFC3576}\s+${IPSEC_STATE}\s+${IPSEC_PROFILE}\s+\/${IPSEC_REGION_STR}\s*$$ -> Record
  ^\s*$$
  ^\s*Accounting Servers\s*$$ -> Acct_Servers
  ^. -> Error

Acct_Servers
  ^\s*Idx\s+Type\s+Server Address\s+Port\s+State\s+Tout\s+MgmtTout\s+RFC3576\s+IPSec - state\/Profile Name\/RadiusRegionString\s*$$
  ^-+
  ^\s*${INDEX}\s+\*\s+${TYPE}\s+${ACCT_SRV_ADDR}\s+${SRV_PORT}\s+${SRV_STATE}\s+${SRV_TIMEOUT}\s+${SRV_MGMT_TIMEOUT}\s+${RFC3576}\s+${IPSEC_STATE}\s+${IPSEC_PROFILE}\s+\/${IPSEC_REGION_STR}\s*$$ -> Record
  ^\s*$$
  ^. -> Error
