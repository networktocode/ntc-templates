Value Filldown,Required SESSION_TYPE (\S+)
Value Filldown CONNECTION (\d+\.\d+\.\d+\.\d+)
Value Filldown INDEX (\d+)
Value Filldown IP_ADDRESS (\d+\.\d+\.\d+\.\d+)
Value Filldown PROTOCOL (\w+)
Value Filldown ENCRYPTION (\w+)
Value Filldown BYTES_TRANSMITTED (\d+)
Value Filldown BYTES_RECIEVED (\d+)
Value Filldown LOGIN_TIME (\d+:\d+:\d+)
Value Filldown LOGIN_TIME_ZONE (\w+)
Value Filldown LOGIN_WEEKDAY (\w+)
Value Filldown LOGIN_MONTH (\w+)
Value Filldown LOGIN_DAY (\d+)
Value Filldown LOGIN_YEAR (\d+)
Value Filldown DURATION (\d+:\d+:\d+)
Value Filldown TOTAL_IKE_SESSIONS (\d+)
Value Filldown TOTAL_IPSEC_SESSIONS (\d+)
Value IKE_CONNECTION_TYPE (IKE)
Value IPSEC_CONNECTION_TYPE (IPSec)
Value SESSION_ID (\d+)
Value UDP_SRC_PORT (\d+)
Value UDP_DST_PORT (\d+)
Value NEGOTIAION_MODE (\w+)
Value AUTHENTICATION_MODE (\w+)
Value ENCRYPTION_METHOD (\w+)
Value HASH_METHOD (\w+)
Value REKEY_INTERVAL (\d+)
Value REKEY_INTERVAL_UNIT (\w+)
Value REKEY_TIME_LEFT (\d+)
Value REKEY_TIME_LEFT_UNIT (\w+)
Value DH_GROUP (\d+)
Value LOCAL_ADDRESS_NETWORK (\d+\.\d+\.\d+\.\d+)
Value LOCAL_ADDRESS_MASK (\d+\.\d+\.\d+\.\d+)
Value REMOTE_ADDRESS_NETWORK (\d+\.\d+\.\d+\.\d+)
Value REMOTE_ADDRESS_MASK (\d+\.\d+\.\d+\.\d+)
Value ENCAPSULATION (\w+)
Value PFS_GROUP (\d+)
Value PACKETS_TRANSMITTED (\d+)
Value PACKETS_RECIEVED (\d+)

Start
  ^Session\s+Type -> Continue.Clearall
  ^Session\s+Type:\s+${SESSION_TYPE}\s+Detailed\s*
  ^\s+Connection\s*:\s+${CONNECTION}\s*
  ^\s+Index\s*:\s+${INDEX}\s+IP\s+Addr\s*:\s+${IP_ADDRESS}\s*
  ^\s+Protocol\s*:\s+${PROTOCOL}\s+Encryption\s*:\s+${ENCRYPTION}\s*
  ^\s+Bytes\s+Tx\s*:\s+${BYTES_TRANSMITTED}\s+Bytes\s+Rx\s*:\s+${BYTES_RECIEVED}\s*
  ^\s+Login\s+Time\s*:\s+${LOGIN_TIME}\s+${LOGIN_TIME_ZONE}\s+${LOGIN_WEEKDAY}\s+${LOGIN_MONTH}\s+${LOGIN_DAY}\s+${LOGIN_YEAR}\s*
  ^\s+Duration\s*:\s+${DURATION}\s*
  ^\s+Filter\s+Name\s*:\s+\S*\s*
  # IKE and IPSEC Session Counts will capture and continue on the first iteration. This data will be recorded on the second iteration if both IKE and IPSec have 0 Sessions
  ^\s+IKE\s+Sessions:\s+${TOTAL_IKE_SESSIONS}\s+IPSec\s+Sessions:\s+${TOTAL_IPSEC_SESSIONS}\s* -> Continue
  ^\s+IKE\s+Sessions:\s+0\s+IPSec\s+Sessions:\s+0\s* -> Record
  ^\s+${IKE_CONNECTION_TYPE}:\s* -> IKE
  ^\s+${IPSEC_CONNECTION_TYPE}:\s* -> IPSec

IKE
  ^\s+Session\s+ID\s*:\s+${SESSION_ID}
  ^\s+UDP\s+Src\s+Port\s*:\s+${UDP_SRC_PORT}\s+UDP\s+Dst\s+Port\s*:\s+${UDP_DST_PORT}\s*
  ^\s+IKE\s+Neg\s+Mode\s*:\s+${NEGOTIAION_MODE}\s+Auth\s+Mode\s*:\s+${AUTHENTICATION_MODE}\s*
  ^\s+Encryption\s*:\s+${ENCRYPTION_METHOD}\s+Hashing\s*:\s+${HASH_METHOD}
  ^\s+Rekey\s+Int\s+\(\w\):\s+${REKEY_INTERVAL}\s+${REKEY_INTERVAL_UNIT}\s+Rekey\s+Left\(\w+\):\s+${REKEY_TIME_LEFT}\s+${REKEY_TIME_LEFT_UNIT}\s*
  ^\s+D\/H\s+Group\s*:\s+${DH_GROUP}\s*
  ^\s*$$ -> Record Start

IPSec
  ^\s+Session\s+ID\s*:\s+${SESSION_ID}
  ^\s+Local\s+Addr\s*:\s+${LOCAL_ADDRESS_NETWORK}\/${LOCAL_ADDRESS_MASK}\s*
  ^\s+Remote\s+Addr\s*:\s+${REMOTE_ADDRESS_NETWORK}\/${REMOTE_ADDRESS_MASK}\s*
  ^\s+Encryption\s*:\s+${ENCRYPTION_METHOD}\s+Hashing\s*:\s+${HASH_METHOD}\s*
  ^\s+Encapsulation\s*:\s+${ENCAPSULATION}\s+PFS\s+Group\s*:\s+${PFS_GROUP}\s*
  ^\s+Rekey\s+Int\s+\(\w\)\s*:\s+${REKEY_INTERVAL}\s+${REKEY_INTERVAL_UNIT}\s+Rekey\s+Left\(\w+\)\s*:\s+${REKEY_TIME_LEFT}\s+${REKEY_TIME_LEFT_UNIT}\s*
  ^\s+Bytes\s+Tx\s*:\s+${BYTES_TRANSMITTED}\s+Bytes\s+Rx\s*:\s+${BYTES_RECIEVED}\s*
  ^\s+Pkts\s+Tx\s*:\s+${PACKETS_TRANSMITTED}\s+Pkts\s+Rx\s*:\s+${PACKETS_RECIEVED}\s*
  ^\s*$$ -> Record Start

EOF