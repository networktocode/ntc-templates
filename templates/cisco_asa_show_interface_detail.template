Value Required INTERFACE (\S+)
Value INTERFACE_NAME (.*)
Value LINK_STATUS (.*)
Value PROTOCOL_STATUS (.*)
Value HARDWARE_TYPE ([\w ]+)
Value BANDWIDTH (\d+\s+\w+)
Value DELAY (\d+\s+\w+)
Value DUPLEX (\w+\-\w+)
Value SPEED (\d+\w+\s\w+)
Value FLOWCONTROL_IN (\w+)
Value FLOWCONTROL_OUT (\w+)
Value DESCRIPTION (.*)
Value ADDRESS ([a-zA-Z0-9]+.[a-zA-Z0-9]+.[a-zA-Z0-9]+)
Value MTU (\d+)
Value IP_ADDRESS (\d+\.\d+\.\d+\.\d+)
Value NET_MASK (\d+\.\d+\.\d+\.\d+)
Value IN_PACKETS (\d+)
Value IN_BYTES (\d+)
Value NO_BUFFER (\d+)
Value BROADCASTS (\d+)
Value RUNTS (\d+)
Value GIANTS (\d+)
Value IN_ERRORS (\d+)
Value CRC (\d+)
Value FRAME (\d+)
Value OVERRUN (\d+)
Value IGNORED (\d+)
Value ABORT (\d+)
Value IN_PAUSE_RESUME (\d+)
Value IN_PAUSE (\d+)
Value IN_RESUME (\d+)
Value L2_DECODE_DROPS (\d+)
Value SWITCH_INGRESS_POLICY_DROPS (\d+)
Value OUT_PACKETS (\d+)
Value OUT_BYTES (\d+)
Value UNDERRUNS (\d+)
Value OUT_PAUSE_RESUME (\d+)
Value OUT_PAUSE (\d+)
Value OUT_RESUME (\d+)
Value OUT_ERRORS (\d+)
Value COLLISIONS (\d+)
Value INTERFACE_RESETS (\d+)
Value LATE_COLLISIONS (\d+)
Value DEFFERED (\d+)
Value RATE_LIMIT_DROPS (\d+)
Value SWITCH_EGRESS_POLICY_DROPS (\d+)
Value IN_RESET_DROPS (\d+)
Value OUT_RESET_DROPS (\d+)
Value VLAN (\d+)
#
# Control Point Interface States
#
Value CONTROL_INTERFACE_NUMBER (\d+)
Value CONTROL_INTERFACE_CONFIG_STATUS (.+)
Value CONTROL_INTERFACE_STATE (.+)
#
# Queue Stats
#
# RXRING
Value List RXRING_NUMBER (\d+)
Value List RXRING_PACKETS (\d+)
Value List RXRING_BYTES (\d+)
Value List RXRING_OVERRUN (\d+)
Value List RXRING_BLOCKS_FREE_CURR (\d+)
Value List RXRING_BLOCKS_FREE_LOW (\d+)
#
# TXRING
Value List TXRING_NUMBER (\d+)
Value List TXRING_PACKETS (\d+)
Value List TXRING_BYTES (\d+)
Value List TXRING_UNDERRUN (\d+)
Value List TXRING_BLOCKS_FREE_CURR (\d+)
Value List TXRING_BLOCKS_FREE_LOW (\d+)


Start
  ^.*Interface ${INTERFACE} "${INTERFACE_NAME}", is ${LINK_STATUS}, line protocol is ${PROTOCOL_STATUS}
  ^\s+Hardware is ${HARDWARE_TYPE} -> Continue
  ^.*BW ${BANDWIDTH}.*DLY ${DELAY}
  ^.*\(${DUPLEX}.*Auto-Speed\(${SPEED}\)
  ^\s+Input flow control is ${FLOWCONTROL_IN}, output flow control is ${FLOWCONTROL_OUT}
  ^.*Description: ${DESCRIPTION}
  ^.*MAC address ${ADDRESS}.*MTU ${MTU}
  ^.*IP address ${IP_ADDRESS}, .*subnet mask ${NET_MASK}
  ^\s+${IN_PACKETS} packets input, ${IN_BYTES} bytes, ${NO_BUFFER} no buffer
  ^\s+Received ${BROADCASTS} broadcasts, ${RUNTS} runts, ${GIANTS} giants
  ^\s+${IN_ERRORS} input errors, ${CRC} CRC, ${FRAME} frame, ${OVERRUN} overrun, ${IGNORED} ignored, ${ABORT} abort
  ^\s+${IN_PAUSE_RESUME} pause/resume input
  ^\s+${IN_PAUSE} pause input, ${IN_RESUME} resume input
  ^\s+${L2_DECODE_DROPS} L2 decode drops
  ^\s+${SWITCH_INGRESS_POLICY_DROPS} switch ingress policy drops
  ^\s+${OUT_PACKETS} packets output, ${OUT_BYTES} bytes, ${UNDERRUNS} underruns
  ^\s+${OUT_PAUSE_RESUME} pause/resume output
  ^\s+${OUT_PAUSE} pause output, ${OUT_RESUME} resume output
  ^\s+${OUT_ERRORS} output errors, ${COLLISIONS} collisions, ${INTERFACE_RESETS} interface resets
  ^\s+${LATE_COLLISIONS} late collisions, ${DEFFERED} deferred
  ^\s+${RATE_LIMIT_DROPS} rate limit drops
  ^\s+${SWITCH_EGRESS_POLICY_DROPS} switch egress policy drops
  ^\s+${IN_RESET_DROPS} input reset drops, ${OUT_RESET_DROPS} output reset drops
  ^\s+VLAN identifier ${VLAN}
  ^\s+Control Point Interface States -> ControlPoint
  ^\s+Queue Stats -> QueueStats
  ^\s+Available for allocation to a context -> Record

ControlPoint
  ^\s+Interface number is ${CONTROL_INTERFACE_NUMBER}
  ^\s+Interface config status is ${CONTROL_INTERFACE_CONFIG_STATUS}
  ^\s+Interface state is ${CONTROL_INTERFACE_STATE} -> Record Start

QueueStats
  ^\s+RX\[${RXRING_NUMBER}\]: ${RXRING_PACKETS} packets, ${RXRING_BYTES} bytes, ${RXRING_OVERRUN} overrun -> RXRingBlocks
  ^\s+TX\[${TXRING_NUMBER}\]: ${TXRING_PACKETS} packets, ${TXRING_BYTES} bytes, ${TXRING_UNDERRUN} underruns -> TXRingBlocks
  ^\s+Control Point Interface States -> ControlPoint

RXRingBlocks
  ^\s+Blocks free curr/low: ${RXRING_BLOCKS_FREE_CURR}/${RXRING_BLOCKS_FREE_LOW} -> QueueStats

TXRingBlocks
  ^\s+Blocks free curr/low: ${TXRING_BLOCKS_FREE_CURR}/${TXRING_BLOCKS_FREE_LOW} -> QueueStats