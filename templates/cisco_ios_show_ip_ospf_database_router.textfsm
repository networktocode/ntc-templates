Value Filldown ROUTER_ID (\d+\.\d+\.\d+\.\d+)
Value Filldown PROCESS_ID (\d+)
Value Filldown AREA (\d+\.\d+\.\d+\.\d+|\d+)
Value Filldown LSA_AGE (\d+)
Value Filldown LSA_OPTIONS (.*)
Value Filldown LSA_TYPE (.*)
Value Filldown LSA_ID (\d+\.\d+\.\d+\.\d+)
Value Filldown LSA_ADV_ROUTER (\d+\.\d+\.\d+\.\d+)
Value Filldown LSA_SEQ_NUMBER (\d+)
Value Filldown LSA_CHECKSUM (\S+)
Value Filldown LSA_LENGTH (\d+)
Value Filldown LSA_NUM_LINKS (\d+)
Value LS_LINK_TYPE ((a Stub Network)|(a Transit Network))
Value LS_LINK_ID (\d+\.\d+\.\d+\.\d+)
Value LS_LINK_DATA (\d+\.\d+\.\d+\.\d+)
Value LS_MTID_METRICS (\d+)
Value LS_TOS_0_METRICS (\d+)

Start
  ^\s+OSPF Router with ID \(${ROUTER_ID}\) \(Process ID ${PROCESS_ID}\)
  ^\s+Router Link States \(Area ${AREA}\) -> LSAInfo
  ^\s*$$
  ^. -> Error

LSAInfo
  ^\s+LS age: ${LSA_AGE}
  ^\s+Options: \(${LSA_OPTIONS}\)
  ^\s+LS Type: ${LSA_TYPE}
  ^\s+Link State ID: ${LSA_ID}
  ^\s+Advertising Router: ${LSA_ADV_ROUTER}
  ^\s+LS Seq Number: ${LSA_SEQ_NUMBER}
  ^\s+Checksum: ${LSA_CHECKSUM}
  ^\s+Length: ${LSA_LENGTH}
  ^\s+Number of Links: ${LSA_NUM_LINKS} -> LSAs
  ^\s*$$
  ^. -> Error

LSAs
  ^\s+Link connected to: ${LS_LINK_TYPE}
  ^\s+\(Link ID\) ((Network\/subnet number)|(Designated Router address)): ${LS_LINK_ID}
  ^\s+\(Link Data\) ((Network Mask)|(Router Interface address)): ${LS_LINK_DATA}
  ^\s+Number of MTID metrics: ${LS_MTID_METRICS}
  ^\s+TOS 0 Metrics: ${LS_TOS_0_METRICS} -> Next.Record
  ^\s -> LSAInfo
  ^\s*$$
  ^. -> Error

EOF