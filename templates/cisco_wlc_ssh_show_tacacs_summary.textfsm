Value Filldown FALLBACK_TEST_INTERVAL (\d+)
Value Required INDEX (\d+)
Value AUTHC_SRV_ADDR (\S+)
Value AUTHZ_SRV_ADDR (\S+)
Value ACCT_SRV_ADDR (\S+)
Value SRV_PORT (\d+)
Value SRV_STATE (\S+)
Value SRV_TIMEOUT (\d+)
Value SRV_MGMT_TIMEOUT (\d+)

Start
  ^\s*Fallback Test:\s*$$
  ^\s+Interval \(in seconds\)\s*\.+\s+${FALLBACK_TEST_INTERVAL}\s*$$
  ^\s*Authentication Servers\s*$$ -> AuthC_Servers
  ^\s*$$
  ^. -> Error

AuthC_Servers
  ^\s*Idx\s+Server Address\s+Port\s+State\s+Tout\s+MgmtTout\s*$$
  ^-+
  ^\s*${INDEX}\s+${AUTHC_SRV_ADDR}\s+${SRV_PORT}\s+${SRV_STATE}\s+${SRV_TIMEOUT}\s+${SRV_MGMT_TIMEOUT}\s*$$ -> Record
  ^\s*Authorization Servers\s*$$ -> AuthZ_Servers
  ^\s*$$
  ^. -> Error

AuthZ_Servers
  ^\s*Idx\s+Server Address\s+Port\s+State\s+Tout\s+MgmtTout\s*$$
  ^-+
  ^\s*${INDEX}\s+${AUTHZ_SRV_ADDR}\s+${SRV_PORT}\s+${SRV_STATE}\s+${SRV_TIMEOUT}\s+${SRV_MGMT_TIMEOUT}\s*$$ -> Record
  ^\s*Accounting Servers\s*$$ -> Acct_Servers
  ^\s*$$
  ^. -> Error

Acct_Servers
  ^\s*Idx\s+Server Address\s+Port\s+State\s+Tout\s+MgmtTout\s*$$
  ^-+
  ^\s*${INDEX}\s+${ACCT_SRV_ADDR}\s+${SRV_PORT}\s+${SRV_STATE}\s+${SRV_TIMEOUT}\s+${SRV_MGMT_TIMEOUT}\s*$$ -> Record
  ^\s*$$
  ^. -> Error
