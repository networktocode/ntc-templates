Value SSID_INDEX (\d+)
Value SSID (\S.+\S)
Value BSSID (\S+)
Value ENABLED (\S+)
Value STATUS (\S+)
Value AUTHENTICATION_MODE (\S+)
Value ENCRYPTION_MODE (\S+)
Value CHANNEL (\d+)
Value CHANNEL_BANDWIDTH_MHZ (\S+)
Value WIFI_STANDARD (\S+)
Value SUPPORTED_MAX_RATE_MBPS (\d+)
Value COUNTRY_CODE (\S+)
Value MAXIMUM_TX_POWER (\d+)
Value CURRENT_TX_POWER_LEVEL (\S+)

Start
  ^\s*SSID\s+number\s*:\s*\d+\s*$$
  ^\s*-+\s*$$
  ^\s*SSID\s+Index\s+:\s*${SSID_INDEX}\s*$$
  ^\s*SSID\s+:\s*${SSID}\s*$$
  ^\s*BSSID\s+:\s*${BSSID}\s*$$
  ^\s*Enable\s+:\s*${ENABLED}\s*$$
  ^\s*Status\s+:\s*${STATUS}\s*$$
  ^\s*Authentication\s+Mode\s+:\s*${AUTHENTICATION_MODE}\s*$$
  ^\s*Encryption\s+Mode\s+:\s*${ENCRYPTION_MODE}\s*$$
  ^\s*Channel\s+:\s*${CHANNEL}(?:\(auto\))?\s*$$
  ^\s*Channel\s+bandwidth\s+:\s*${CHANNEL_BANDWIDTH_MHZ}\s*MHz$$
  ^\s*Standard\s+:\s*${WIFI_STANDARD}\s*$$
  ^\s*Supported\s+Max\s+Rate\s+:\s*${SUPPORTED_MAX_RATE_MBPS}\s*M\s*$$
  ^\s*Country\s+Code\s+:\s*${COUNTRY_CODE}\s*$$
  ^\s*Maximum\s+Tx-Power\s+:\s*${MAXIMUM_TX_POWER}\s+dBm\s+\(\d+\s+mW\)\s*$$
  ^\s*Current\s+Tx-Power\s+Level\s+:\s*${CURRENT_TX_POWER_LEVEL}%\s*$$ -> Record
  ^\s+$$
  ^\s*success!\s*$$
  ^. -> Error
