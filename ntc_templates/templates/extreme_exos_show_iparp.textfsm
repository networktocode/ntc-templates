Value VR (\S+)
Value DESTINATION (\d+\.\d+\.\d+\.\d+)
Value MAC_ADDRESS ([0-9A-Fa-f:]+|incomplete)
Value PORT (\S+)
Value STATIC (\S+)
Value AGE (\d+)
Value VLAN (\S+)
Value VID (\S+)

Start
  ^VR\s+Destination\s+Mac\s+Age\s+Static\s+VLAN\s+VID\s+Port -> Start_record
  ^. -> Error

Start_record
  ^\s*---
  ^${VR}\s+${DESTINATION}\s+\(*${MAC_ADDRESS}\)*\s+${AGE}\s+${STATIC}\s+${VLAN}\s+${VID}(\s+${PORT})?\s*$$ -> Record
  ^\s*(Dynamic|Pending)\s+Entries\s*:
  ^\s*(In|Out|Failed)\s+Requests?\s*:
  ^\s*Proxy\s+Answered\s*:
  ^\s*Rx\s+Error\s*:
  ^\s*Rejected\s+(Count|Port)\s*:
  ^\s*Max\s+((ARP|Pending|Proxy)\s+)?[Ee]ntries\s*:
  ^\s*ARP\s+address\s+check\s*:
  ^\s*Timeout\s*:
  ^\s*Distributed\s+mode\s*:
  ^\s*ARP\s+Sender-Mac\s+Learning\s*:
  ^\s*Locktime\s*:
  ^\s*(Retransmit|Reachable)\s+Time\s*:
  ^\s*Fast\s+Convergence\s*:
  ^\s*ARP\s+Global\s+Settings\s*$$
  ^\s*$$
  ^. -> Error
