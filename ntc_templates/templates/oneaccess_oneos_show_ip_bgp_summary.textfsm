Value Filldown,Required ROUTER_ID ([0-9a-f:\.]+)
Value Filldown LOCAL_AS (\d+(\.\d+)?)
Value Filldown VRF (\w+)
Value Filldown RIB_ENTRIES (\d+)
Value Filldown RIB_MEMORY (\d+\s\S+)
Value Filldown PEERS (\d+)
Value Filldown BGP_MEMORY (\d+\s\S+)
Value Filldown AS_PATH_ENTRIES (\d+)
Value Filldown COMMUNTIY_ENTRIES (\d+)
Value BGP_NEIGH (\d+\.\d+\.\d+\.\d+)
Value ADDR_FAMILY (\d+)
Value NEIGH_AS (\d+)
Value MSG_RCVD (\d+)
Value MSG_SENT (\d+)
Value TBL_VER (\d+)
Value IN_Q (\d+)
Value OUT_Q (\d+)
Value UP_DOWN (\S+?)
Value STATE_PFXRCD (\S+\s+\S+|\S+)
Value Fillup TOTAL_NEIGHBORS (\d+)

Start
  ^BGP\srouter\sidentifier\s${ROUTER_ID},\slocal\sAS\snumber\s${LOCAL_AS},?\s+vrf\s\(?${VRF}\)?
  ^RIB\sentries\s${RIB_ENTRIES},\susing\s${RIB_MEMORY}\sof\smemory
  ^Peers\s${PEERS},\susing\s${BGP_MEMORY}\sof\smemory
  ^${AS_PATH_ENTRIES}\sBGP\sAS-PATH\sentries
  ^${COMMUNTIY_ENTRIES}\sBGP\scommunity\sentries
  ^Neighbor\s
  ^${BGP_NEIGH}\s+${ADDR_FAMILY}\s+${NEIGH_AS}\s+${MSG_RCVD}\s+${MSG_SENT}\s+${TBL_VER}\s+${IN_Q}\s+${OUT_Q}\s+${UP_DOWN}\s+${STATE_PFXRCD} -> Record
  ^Total\snumber\sof\sneighbors\s${TOTAL_NEIGHBORS}
  ^\s*$$
  ^. -> Error

EOF
