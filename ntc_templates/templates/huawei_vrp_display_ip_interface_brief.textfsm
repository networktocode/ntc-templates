Value Filldown PHYSICAL_UP (\d+)
Value Filldown PHYSICAL_DOWN (\d+)
Value Filldown PROTOCOL_UP (\d+)
Value Filldown PROTOCOL_DOWN (\d+)
Value Required INTERFACE (\S+)
Value IP_ADDRESS_MASK ([\w\./]+)
Value PHYSICAL ([\w\*]+)
Value PROTOCOL ([\w\(\)]+)

Start
  ^[\d\-]+\s+[\d:\.]+\s+[\d:\+]+$$
  ^(?:\*|\^|!)down:
  ^\(\w+\):\s+\S+
  ^The number of interface that is UP in Physical is ${PHYSICAL_UP}
  ^The number of interface that is DOWN in Physical is ${PHYSICAL_DOWN}
  ^The number of interface that is UP in Protocol is ${PROTOCOL_UP}
  ^The number of interface that is DOWN in Protocol is ${PROTOCOL_DOWN}
  ^Interface\s+IP\s+Address/Mask\s+Physical\s+Protocol -> INTERFACE_BRIEF
  ^. -> Error

INTERFACE_BRIEF
  ^${INTERFACE}\s+${IP_ADDRESS_MASK}\s+${PHYSICAL}\s+${PROTOCOL} -> Record
  ^. -> Error
