Value Filldown PHYSICAL_UP (\d+)
Value Filldown PHYSICAL_DOWN (\d+)
Value Filldown PROTOCOL_UP (\d+)
Value Filldown PROTOCOL_DOWN (\d+)
Value Required INTERFACE (\S+)
Value IP_ADDRESS_MASK (\S+)
Value PHYSICAL ([\w\*]+)
Value PROTOCOL (\S+)
Value VPN (\S+)

Start
  ^[\d\-]+\s+[\d:\.]+\s+[\d:\+]+
  ^\*down: administratively down
  ^\^down: standby
  ^\(l\): loopback
  ^\(s\): spoofing
  ^\(d\): Dampening Suppressed
  ^\(E\): E\-Trunk down
  ^The number of interface that is UP in Physical is ${PHYSICAL_UP}
  ^The number of interface that is DOWN in Physical is ${PHYSICAL_DOWN}
  ^The number of interface that is UP in Protocol is ${PROTOCOL_UP}
  ^The number of interface that is DOWN in Protocol is ${PROTOCOL_DOWN}
  ^Interface\s+IP\s+Address/Mask\s+Physical\s+Protocol\s+VPN -> INTERFACE_BRIEF1
  ^Interface\s+IP\s+Address/Mask\s+Physical\s+Protocol -> INTERFACE_BRIEF2
  ^. -> Error

INTERFACE_BRIEF1
  ^${INTERFACE}\s+${IP_ADDRESS_MASK}\s+${PHYSICAL}\s+${PROTOCOL}\s+${VPN} -> Record
  ^. -> Error

INTERFACE_BRIEF2
  ^${INTERFACE}\s+${IP_ADDRESS_MASK}\s+${PHYSICAL}\s+${PROTOCOL} -> Record
  ^. -> Error