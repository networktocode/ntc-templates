Value SOFTWARE_VERSION (\S+)
Value SOFTWARE_RELEASE (\S+)
Value MODEL (.+?)
Value UPTIME (.+?)
Value LAST_REBOOT_REASON (.+)
Value SLOT_NUMBER (\d+)


Start
  ^\s*(HPE?\s+)?Comware\s+Software,\s+Version\s+${SOFTWARE_VERSION},\s+(Feature|Release)\s+${SOFTWARE_RELEASE}
  ^\s*(H3C|HP)\s+Comware\s+
  ^\s*Copyright\s+\(c\)
  ^\s*(HPE?|H3C)\s+${MODEL}\s+(\d+-slot|EI|HI|Switch|\(\d+W\)).+uptime\s+is\s+${UPTIME}$$
  ^\s*(HPE?|H3C)\s+${MODEL}\s+uptime\s+is\s+${UPTIME}$$
  ^\s*Hardware\s+Version
  ^\s*Bootrom\s+Version
  ^\s*CPLD
  ^\s*\d+[A-Z]\s+bytes
  ^\s*Last\s+reboot\s+reason\s+:\s+${LAST_REBOOT_REASON}
  ^\s*Boot\s+image(\s+version)?:
  ^\s*Compiled\s*.+
  ^\s*System\s+image(\s+version)?: -> Record
  ^\s*Feature\s+image
  ^\s*Patch\s+image\(s\)\s*list:
  ^\s*flash:
  ^\s*(HPE?|H3C)\s+.+
  ^\s*Slot\s*${SLOT_NUMBER}:?\s* -> Slot
  ^\s*MPU\([MS]\)\s+(Slot\s*)?${SLOT_NUMBER}:?\s* -> Slot
  ^\s*LPU\s+${SLOT_NUMBER}:?\s* -> Slot
  ^\s*\[SubSlot\s*\d+\] 
  ^\s*$$
  ^. -> Error

Slot
  ^\s*Slot\s*${SLOT_NUMBER}:?\s*
  ^\s*MPU\([MS]\)\s+(Slot\s*)?${SLOT_NUMBER}:?\s*
  ^\s*LPU\s+${SLOT_NUMBER}:?\s*
  ^\s*Uptime\s+is\s+${UPTIME}$$
  ^\s*Release\s+Version:
  ^\s*BOARD\s+TYPE:
  ^\s*HPE?\s+${MODEL}\s+(Switch|with)
  ^\s*${MODEL}\s+(Switch|with)
  ^\s*(DRAM|FLASH|NVRAM):
  ^\s*PCB
  ^\s*(Basic|Extended)\s+BootWare\s+Version
  ^\s*FPGA
  ^\s*Boot[rR]om\s+Version:?
  ^\s*(Bootware|Hardware)\s+Version
  ^\s*CPLD
  ^\s*Patch\s+Version\s*:
  ^\s*Reboot\s+Cause\s*:\s+${LAST_REBOOT_REASON}
  ^\s*Last\s+reboot\s+reason\s+:\s+${LAST_REBOOT_REASON}
  ^\s*\d+[A-Z]\s+bytes
  ^\s*Config\s+Register
  ^\s*\[SubSlot\s*\d+\] -> Record 
  ^\s*$$ -> Record
  ^. -> Error