# These are top level summary values we "Filldown" to each record.
# This is because TextFSM does not support storing these as a separate dicitonary within the output
# Global values from line 1
Value Filldown GLOBAL_TIMESTAMP (\d+:\d+:\d+)
Value Filldown GLOBAL_USERS (\d+)
Value Filldown GLOBAL_LOAD_AVERAGE_1_MINUTES (\d+[.]\d+)
Value Filldown GLOBAL_LOAD_AVERAGE_5_MINUTES (\d+[.]\d+)
Value Filldown GLOBAL_LOAD_AVERAGE_15_MINUTES (\d+[.]\d+)
# Global values from line 2
Value Filldown GLOBAL_TASKS_TOTAL (\d+)
Value Filldown GLOBAL_TASKS_RUNNING (\d+)
Value Filldown GLOBAL_TASKS_SLEEPING (\d+)
Value Filldown GLOBAL_TASKS_STOPPED (\d+)
Value Filldown GLOBAL_TASKS_ZOMBIE (\d+)
# Global values from line 3
Value Filldown GLOBAL_CPU_PERCENT_USER (\d+[.]\d+)
Value Filldown GLOBAL_CPU_PERCENT_SYSTEM (\d+[.]\d+)
Value Filldown GLOBAL_CPU_PERCENT_NICE (\d+[.]\d+)
Value Filldown GLOBAL_CPU_PERCENT_IDLE (\d+[.]\d+)
Value Filldown GLOBAL_CPU_PERCENT_IOWAIT (\d+[.]\d+)
Value Filldown GLOBAL_CPU_PERCENT_HI (\d+[.]\d+)
Value Filldown GLOBAL_CPU_PERCENT_SI (\d+[.]\d+)
Value Filldown GLOBAL_CPU_PERCENT_STOLEN (\d+[.]\d+)  
# Global values from line 4    
# GLOBAL_MEM_USED is not included because different EOS versions put the used and free memory values in a different order and due
# to regex/TextFSM limitations it's not feasible to get both values, so we choose GLOBAL_MEM_FREE     
Value Filldown GLOBAL_MEM_UNIT (KiB|MiB|GiB|TiB)
Value Filldown GLOBAL_MEM_TOTAL (\d+[.]?\d+)
Value Filldown GLOBAL_MEM_FREE (\d+[.]?\d+)
Value Filldown GLOBAL_MEM_BUFFERS (\d+[.]?\d+)
# Global values from line 5 (swap memory statistics) are skipped because of heavy variance between EOS versions
# Per process records start here
Value Required PID (\d+)
Value USER (\S+)
Value PRIORITY (\d+|rt)
Value NICE (-?\d+)
Value VIRTUAL_MEMORY_SIZE (\d+m?)
Value RESIDENT_MEMORY_SIZE (\d+m?)
Value SHARED_MEMORY_SIZE (\d+m?)
Value PROCESS_STATUS (D|I|R|S|T|t|Z)
  # Below description of process status from https://man7.org/linux/man-pages/man1/top.1.html
  # 29. S  --  Process Status
  #            The status of the task which can be one of:
  #                D = uninterruptible sleep
  #                I = idle
  #                R = running
  #                S = sleeping
  #                T = stopped by job control signal
  #                t = stopped by debugger during trace
  #                Z = zombie
Value PERCENT_CPU (\d+[.]\d+)
Value PERCENT_MEMORY (\d+[.]\d+)
Value CPU_TIME (\d+:\d+\S+)
Value COMMAND (\S+)


Start  
  ^top - ${GLOBAL_TIMESTAMP} up .*,\s+${GLOBAL_USERS} use.*,\s+load average:\s+${GLOBAL_LOAD_AVERAGE_1_MINUTES},\s+${GLOBAL_LOAD_AVERAGE_5_MINUTES},\s+${GLOBAL_LOAD_AVERAGE_15_MINUTES}$$
  ^Tasks:\s+${GLOBAL_TASKS_TOTAL} total,\s+${GLOBAL_TASKS_RUNNING} running,\s+${GLOBAL_TASKS_SLEEPING} sleeping,\s+${GLOBAL_TASKS_STOPPED} stopped,\s+${GLOBAL_TASKS_ZOMBIE} zombie$$
  ^.*Cpu.s.:\s+${GLOBAL_CPU_PERCENT_USER}.us,\s+${GLOBAL_CPU_PERCENT_SYSTEM}.sy,\s+${GLOBAL_CPU_PERCENT_NICE}.ni,\s+${GLOBAL_CPU_PERCENT_IDLE}.id,\s+${GLOBAL_CPU_PERCENT_IOWAIT}.wa,\s+${GLOBAL_CPU_PERCENT_HI}.hi,\s+${GLOBAL_CPU_PERCENT_SI}.si,\s+${GLOBAL_CPU_PERCENT_STOLEN}.st$$
  ^${GLOBAL_MEM_UNIT}\s+Mem.*:\s+${GLOBAL_MEM_TOTAL}\s+total,.*\s+${GLOBAL_MEM_FREE}\s+free,.*\s+${GLOBAL_MEM_BUFFERS}\s+buff.*$$
  ^.*Swap.*total,.*free,.*$$
  ^$$     
  ^\s*PID\s+USER\s+PR\s+NI\s+VIRT\s+RES\s+SHR\s+S\s+.CPU\s+.MEM\s+TIME.\s+COMMAND$$
  ^\s*${PID}\s+${USER}\s*${PRIORITY}\s*${NICE}\s+${VIRTUAL_MEMORY_SIZE}\s+${RESIDENT_MEMORY_SIZE}\s+${SHARED_MEMORY_SIZE}\s+${PROCESS_STATUS}\s+${PERCENT_CPU}\s+${PERCENT_MEMORY}\s+${CPU_TIME}\s+${COMMAND}$$ -> Record
  ^. -> Error