# These are top level summary values we "Filldown" to each record.
# This is because TextFSM does not support storing these as a separate dicitonary within the output
Value Filldown GLOBAL_TIMESTAMP (\d{2}:\d{2}:\d{2})
Value Filldown GLOBAL_USERS (\d+)
Value Filldown GLOBAL_TASKS_TOTAL (\d+)
Value Filldown GLOBAL_TASKS_RUNNING (\d+)
Value Filldown GLOBAL_TASKS_SLEEPING (\d+)
Value Filldown GLOBAL_TASKS_STOPPED (\d+)
Value Filldown GLOBAL_TASKS_ZOMBIE (\d+)
Value Filldown GLOBAL_LOAD_AVERAGE_1_MINUTES (\d+[.]\d+)
Value Filldown GLOBAL_LOAD_AVERAGE_5_MINUTES (\d+[.]\d+)
Value Filldown GLOBAL_LOAD_AVERAGE_15_MINUTES (\d+[.]\d+)
Value Filldown GLOBAL_PROCESS_TIME_USER (\d+[.]\d+)
Value Filldown GLOBAL_PROCESS_TIME_SYSTEM (\d+[.]\d+)
Value Filldown GLOBAL_PROCESS_TIME_NICE (\d+[.]\d+)
Value Filldown GLOBAL_PROCESS_TIME_IDLE (\d+[.]\d+)
Value Filldown GLOBAL_PROCESS_TIME_IOWAIT (\d+[.]\d+)
Value Filldown GLOBAL_PROCESS_TIME_HI (\d+[.]\d+)
Value Filldown GLOBAL_PROCESS_TIME_SI (\d+[.]\d+)
Value Filldown GLOBAL_PROCESS_TIME_STOLEN (\d+[.]\d+)           
Value Filldown GLOBAL_MEM_UNIT (\S+)
Value Filldown GLOBAL_SWAP_UNIT (\S+)
Value Filldown GLOBAL_MEM_TOTAL (\d+)
Value Filldown GLOBAL_MEM_USED (\d+)
Value Filldown GLOBAL_MEM_FREE (\d+)
Value Filldown GLOBAL_MEM_BUFFERS (\d+)
Value Filldown GLOBAL_SWAP_TOTAL (\d+)
Value Filldown GLOBAL_SWAP_USED (\d+)
Value Filldown GLOBAL_SWAP_FREE (\d+)
Value Filldown GLOBAL_SWAP_CACHED (\d+)
# Per process records start here
Value Required PID (\d+)
Value USER (\S+)
Value PRIORITY (\d+|rt)
Value NICE (-?\d+)
Value VIRTUAL_MEMORY_SIZE (\d+m?)
Value RESIDENT_MEMORY_SIZE (\d+m?)
Value SHARED_MEMORY_SIZE (\d+m?)
Value PROCESS_STATUS (D|I|R|S|T|t|Z)
  # Below description of process status from https://man7.org/linux/man-pages/man1/top.1.html
  # 29. S  --  Process Status
  #            The status of the task which can be one of:
  #                D = uninterruptible sleep
  #                I = idle
  #                R = running
  #                S = sleeping
  #                T = stopped by job control signal
  #                t = stopped by debugger during trace
  #                Z = zombie
Value PERCENT_CPU (\d+[.]\d+)
Value PERCENT_MEMORY (\d+[.]\d+)
Value CPU_TIME (\d+:\d+[.]\d+)
Value COMMAND (\S+)


Start
  ^top - ${GLOBAL_TIMESTAMP} up .*,\s+${GLOBAL_USERS} users,\s+load average:\s+${GLOBAL_LOAD_AVERAGE_1_MINUTES},\s+${GLOBAL_LOAD_AVERAGE_5_MINUTES},\s+${GLOBAL_LOAD_AVERAGE_15_MINUTES}
  ^Tasks:\s+${GLOBAL_TASKS_TOTAL} total,\s+${GLOBAL_TASKS_RUNNING} running,\s+${GLOBAL_TASKS_SLEEPING} sleeping,\s+${GLOBAL_TASKS_STOPPED} stopped,\s+${GLOBAL_TASKS_ZOMBIE} zombie
  ^%Cpu.s.:\s+${GLOBAL_PROCESS_TIME_USER} us,\s+${GLOBAL_PROCESS_TIME_SYSTEM} sy,\s+${GLOBAL_PROCESS_TIME_NICE} ni,\s+${GLOBAL_PROCESS_TIME_IDLE} id,\s+${GLOBAL_PROCESS_TIME_IOWAIT} wa,\s+${GLOBAL_PROCESS_TIME_HI} hi,\s+${GLOBAL_PROCESS_TIME_SI} si,\s+${GLOBAL_PROCESS_TIME_STOLEN} st
  ^${GLOBAL_MEM_UNIT} Mem:\s+${GLOBAL_MEM_TOTAL} total,\s+${GLOBAL_MEM_USED} used,\s+${GLOBAL_MEM_FREE} free,\s+${GLOBAL_MEM_BUFFERS} buffers
  ^${GLOBAL_SWAP_UNIT} Swap:\s+${GLOBAL_SWAP_TOTAL} total,\s+${GLOBAL_SWAP_USED} used,\s+${GLOBAL_SWAP_FREE} free,\s+${GLOBAL_SWAP_CACHED} cached
  ^  PID USER      PR  NI  VIRT  RES  SHR S  %CPU %MEM    TIME.  COMMAND
  ^$$
  ^\s*${PID} ${USER}\s*${PRIORITY}\s*${NICE}\s+${VIRTUAL_MEMORY_SIZE}\s+${RESIDENT_MEMORY_SIZE}\s+${SHARED_MEMORY_SIZE} ${PROCESS_STATUS}\s+${PERCENT_CPU}\s+${PERCENT_MEMORY}\s+${CPU_TIME} ${COMMAND}$$ -> Record
  ^. -> Error