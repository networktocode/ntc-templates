Value Filldown INTERFACE (.*)
Value Filldown SERVICE_POLICY_OUT_PARENT (\S+)
Value Filldown CLASS_PARENT (\S+)
Value Filldown CLASS_PARENT_PRIO (.*\S)
Value Filldown SERVICE_POLICY_OUT_CHILD (\S+)
Value CLASS_CHILD (\S+)
Value PACKETS (\d+)
Value Required BYTES (\d+)
Value DSCP_REMARKED (\d+)
Value MEAN_INTPUT_RATE_BITS_SECOND (\d+)
Value Fillup TOTAL_BW_KBS (\d+)
Value Fillup TOTAL_QUEUE_LENGTH (\d+)
Value Fillup TOTAL_MEAN_INPUT_RATE_BPS (\d+)
Value Fillup TOTAL_MEAN_OUTPUT_RATE_BPS (\d+)
Value Fillup TOTAL_OUTPUT_PACKETS (\d+)
Value Fillup TOTAL_OUTPUT_PACKETS_DROPPED (\d+)
Value Fillup TOTAL_OUTPUT_PACKETS_DROPPED_PCT (\d+)
Value Fillup TOTAL_OUTPUT_BYTES (\d+)
Value Fillup TOTAL_OUTPUT_BYTES_DROPPED (\d+)
Value Fillup TOTAL_OUTPUT_BYTES_DROPPED_PCT (\d+)
Value QOS_BYTES_OUT (\d+)
Value QOS_BYTES_DROPPED (\d+)
Value QOS_BYTES_DROPPED_PCT (\d+)
Value QOS_AVG_INPUT_BPS (\d+)
Value QOS_AVG_OUTPUT_BPS (\d+)
Value QOS_PKTS_OUT (\d+)
Value QOS_PKTS_DROPPED (\d+)
Value QOS_PKTS_DROPPED_PCT (\d+)
Value QOS_BW_KB (\d+)
Value QOS_BURST_B (\d+)
Value QOS_Q_LENGTH (\d+)
Value QOS_Q_LIMIT (\d+)
Value Fillup QOS_REMAINING_BW_SHARE (\d+)
Value Fillup QOS_EXCESS_RATE_PRIO (\d+)
Value Fillup QOS_EXCESS_PACKETS (\d+)

# each record is either part of the traffic stats output
#   these records do not have any QOS values filled in
# or the queueing output
#   these records will have QOS values filled in
Start
  ^${INTERFACE}:\sservice-policy\soutput\s${SERVICE_POLICY_OUT_PARENT}
  ^traffic\sstatistics: -> TrafficStats
  ^output\squeuing\sstatistics: -> OutputQueuingStats
  ^.*


TrafficStats
  ^\s\sClass\s'${CLASS_PARENT}':
  ^\s\s\s\s${PACKETS}\spackets,\s${BYTES}\sbytes,\s${DSCP_REMARKED}\sdscp\sremarked,\smean\sinput\srate\s${MEAN_INTPUT_RATE_BITS_SECOND}\sbits/s -> Record
  ^\s\s\s\sService-policy\s${SERVICE_POLICY_OUT_CHILD}\s:
  ^\s\s\s\s\s\sClass\s'${CLASS_CHILD}':
  ^\s\s\s\s\s\s\s\s${PACKETS}\spackets,\s${BYTES}\sbytes,\s${DSCP_REMARKED}\sdscp\sremarked,\smean\sinput\srate\s${MEAN_INTPUT_RATE_BITS_SECOND}\sbits/s -> Record
  ^\s*$$ -> Start

OutputQueuingStats
  ^\s\s(\s\s\s\s)?Class\s'${CLASS_PARENT}':\s${CLASS_PARENT_PRIO}
  ^\s\s\s\s(\s\s\s\s)?bandwidth\s${QOS_BW_KB}\skb/s,\sburst\s${QOS_BURST_B}\sbytes,\squeue\slength/limit\s${QOS_Q_LENGTH}/${QOS_Q_LIMIT} 
  ^\s\s\s\s(\s\s\s\s)?mean\sinput\srate\s${QOS_AVG_INPUT_BPS}\sbits/s,\smean\soutput\srate\s${QOS_AVG_OUTPUT_BPS}\sbits/s
  ^\s\s\s\s(\s\s\s\s)?packets\soutput\s${QOS_PKTS_OUT},\spackets\sdropped\s${QOS_PKTS_DROPPED}\s\(${QOS_PKTS_DROPPED_PCT}%\)
  ^\s\s\s\s(\s\s\s\s)?remaining-bandwidth\sshare\s${QOS_REMAINING_BW_SHARE},\sexcess-rate-priority\s${QOS_EXCESS_RATE_PRIO},\sexcess\spackets\ssent\s${QOS_EXCESS_PACKETS}
  ^\s\s\s\s(\s\s\s\s)?bytes\soutput\s${QOS_BYTES_OUT},\sbytes\sdropped\s${QOS_BYTES_DROPPED}\s\(${QOS_BYTES_DROPPED_PCT}%\) -> Continue
  ^\s\s\s\s(\s\s\s\s)?bytes\soutput\s${BYTES},\sbytes\sdropped\s\d+\s\(\d+%\) -> Record
  ^\s\sInterface\stotal: -> StatsInterfaceTotal
  ^.*

StatsInterfaceTotal
  ^\s+bandwidth\s${TOTAL_BW_KBS}\skb/s,\squeue\slength\s${TOTAL_QUEUE_LENGTH}
  ^\s+mean\sinput\srate\s${TOTAL_MEAN_INPUT_RATE_BPS}\sbits/s,\smean\soutput\srate\s${TOTAL_MEAN_OUTPUT_RATE_BPS}\sbits/s
  ^\s+packets\soutput\s${TOTAL_OUTPUT_PACKETS},\spackets\sdropped\s${TOTAL_OUTPUT_PACKETS_DROPPED}\s\(${TOTAL_OUTPUT_PACKETS_DROPPED_PCT}%\)
  ^\s+bytes\soutput\s${TOTAL_OUTPUT_BYTES},\sbytes\sdropped\s${TOTAL_OUTPUT_BYTES_DROPPED}\s\(${TOTAL_OUTPUT_BYTES_DROPPED_PCT}%\)
  ^\s*$$ -> Start
