Value SL_ENABLED (\S+)
Value SL_TRANSPORT (.*)
Value SL_URL (\S+)
Value SL_PROXY_ADDRESS (\S+)
Value SL_PROXY_PORT (\d+)
Value SL_PROXY_USERNAME (\S+)
Value SL_PROXY_PASSWORD (\S+)
Value SL_VRF (\S+)
Value SL_REG_STATUS (\S+)
Value SL_VIRT_ACCT (.*)
Value SL_SMART_ACCT (.*?)
Value SL_LICENSE_AUTH (.*)

Start
  ^Smart\s+Licensing\s+is\s+${SL_ENABLED}\s*$$
  ^Smart\s+Licensing\s+Using\s+Policy:\s*$$ -> UsingPolicy
  ^Transport:\s*$$ -> Transport
  ^Registration:\s*$$ -> Registration
  ^Account\s+Information:\s*$$ -> Registration
  ^License\s+Authorization:\s* -> LicenseAuth
  # match other sections
  ^\s*(\S+\s*)+$$
  ^\s*$$
  ^. -> Error

UsingPolicy
  ^\s+Status:\s+${SL_ENABLED}\s*$$
  ^\s*$$ -> Start
  ^. -> Error

Transport
  ^\s+Type:\s+${SL_TRANSPORT}\s*$$
  ^\s+URL:\s+${SL_URL}\s*$$
  ^\s+Address:\s+${SL_PROXY_ADDRESS}\s*$$
  ^\s+Port:\s+${SL_PROXY_PORT}\s*$$
  ^\s+Username:\s+${SL_PROXY_USERNAME}\s*$$
  ^\s+Password:\s+${SL_PROXY_PASSWORD}\s*$$
  ^\s+VRF:\s+${SL_VRF}\s*$$
  ^\s+(\S+\s*)+$$
  ^\s*$$ -> Start
  ^. -> Error

Registration
  ^\s+Status:\s+${SL_REG_STATUS}\s*$$
  ^\s+Smart\s+Account:\s+${SL_SMART_ACCT}(\s+As\s+of(\s+\S+)+)?\s*$$
  ^\s+Virtual\s+Account:\s+${SL_VIRT_ACCT}\s*$$
  # match other lines
  ^\s+(\S+\s*)+$$
  ^\s*$$ -> Start
  ^. -> Error

LicenseAuth
  ^\s+Status:\s+${SL_LICENSE_AUTH}(\s+\S+){6}$$
  # match other lines
  ^\s+(\S+\s*)+$$
  ^\s*$$ -> Start
  ^. -> Error
