Value NODE (\S+)
Value MEMORY_FORMAT (\w)
Value SWAP_FORMAT (\w)
Value MEMORY_ACTIVE (\S+)
Value MEMORY_FREE (\S+)
Value MEMORY_USED (\S+)
Value MEMORY_BUFFER (\S+)
Value SWAP_TOTAL (\S+)
Value SWAP_FREE (\S+)
Value SWAP_USED (\S+)
Value SWAP_AVAIL (\S+)

Start
  ^-+\s*$$ -> TYPE1
  ^last\s*pid: -> TYPE2

# JUNOS EVO template
TYPE1
  ^-+\s*$$
  ^node:\s+${NODE}
  ^top\s+
  ^Tasks:\s+
  ^%Cpu\(s\):\s+
  ^${MEMORY_FORMAT}iB\s+M(em|EMORY)\s+:\s+${MEMORY_ACTIVE}\s+total,\s+${MEMORY_FREE}\s+free,\s+${MEMORY_USED}\s+used,\s+${MEMORY_BUFFER}\s+buff/cache\s*
  ^${SWAP_FORMAT}iB\s+Swap:\s+${SWAP_TOTAL}\s+total,\s+${SWAP_FREE}\s+free,\s+${SWAP_USED}\s+used\.\s+${SWAP_AVAIL}\s+avail\s+M(em|EMORY)\s* -> Record
  ^{(master|backup)}\s*$$
  ^\s*$$
  ^. -> Error

# Junos Classic template
TYPE2
  ^last\s+pid:
  ^\d+\s+processes:\s+\d+\s+running,\s+\d+\s+sleeping,\s+\d+\s+zombie,\s+\d+\s+waiting\s*$$
  ^CPU:\s+\d+\.\d+\%\s+user,\s+\d+\.\d+\%\s+nice,\s+\d+\.\d+\%\s+system,\s+\d+\.\d+\%\s+interrupt,\s+\d+\.\d+\%\s+idle\s*$$
  ^Mem:\s+${MEMORY_ACTIVE}${MEMORY_FORMAT}\s+Active,\s+\S+\s+Inact,\s+\S+\s+Wired,\s+${MEMORY_BUFFER}\S+\s+Buf,\s+${MEMORY_FREE}\S+\s+Free\s*$$
  ^Swap:\s+${SWAP_TOTAL}${SWAP_FORMAT}\s+Total,\s+${SWAP_FREE}\S+\s+Free\s*$$
  ^. -> Error
