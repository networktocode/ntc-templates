Value Filldown UPTIME (\d+:\d+:\d+[\s\w\d]+)
Value Filldown USERS (\d+)
Value Filldown LOAD_AVG1M ([\d\.]+)
Value Filldown LOAD_AVG5M ([\d\.]+)
Value Filldown LOAD_AVG15M ([\d\.]+)
Value Filldown TASKS_TOTAL (\d+)
Value Filldown TASKS_RUN (\d+)
Value Filldown TASKS_SLEEP (\d+)
Value Filldown TASKS_STOPPED (\d+)
Value Filldown TASKS_ZOMBIE (\d+)
Value Filldown CPU_US ([\d\.]+)
Value Filldown CPU_SY ([\d\.]+)
Value Filldown CPU_NI ([\d\.]+)
Value Filldown CPU_ID ([\d\.]+)
Value Filldown CPU_WA ([\d\.]+)
Value Filldown CPU_HI ([\d\.]+)
Value Filldown CPU_SI ([\d\.]+)
Value Filldown CPU_ST ([\d\.]+)
Value Filldown MEMORY_TOTAL (\d+)
Value Filldown MEMORY_USED (\d+)
Value Filldown MEMORY_FREE (\d+)
Value Filldown MEMORY_CACHE (\d+)
Value Filldown SWAP_TOTAL (\d+)
Value Filldown SWAP_USED (\d+)
Value Filldown SWAP_FREE (\d+)
Value Filldown SWAP_AVAIL (\d+)
Value Key,Required PID (\d+)
Value USER ([\w\-]+)
Value PR ([\-\d\w]+)
Value NI ([\d\w\-]+)
Value VIRT ([\d]+)
Value RES ([\d]+)
Value SHR ([\d]+)
Value S (\w)
Value CPU ([\d\.]+)
Value MEM ([\d\.]+)
Value TIME ([\d\.\:]+)
Value COMMAND ([\w\d\-\:\./]+)

Start
 ^top - ${UPTIME},  ${USERS} users,  load average: ${LOAD_AVG1M}, ${LOAD_AVG5M}, ${LOAD_AVG15M}
 ^Tasks: ${TASKS_TOTAL} total,  ${TASKS_RUN} running, ${TASKS_SLEEP} sleeping,  ${TASKS_STOPPED} stopped,  ${TASKS_ZOMBIE} zombie
 ^[Cpus\(\):]+\s+${CPU_US}[\s%\w\,]+\s${CPU_SY}[\s%\w\,]+\s${CPU_NI}[\s%\w\,]+\s${CPU_ID}[\s%\w\,]+\s${CPU_WA}[\s%\w\,]+\s${CPU_HI}[\s%\w\,]+\s${CPU_SI}[\s%\w\,]+\s${CPU_ST}
 ^KiB Mem :\s+${MEMORY_TOTAL} total,\s+${MEMORY_USED} used,\s+${MEMORY_FREE} free,\s+${MEMORY_CACHE} buff/cache
 ^KiB Swap:\s+${SWAP_TOTAL} total,\s+${SWAP_USED} used,\s+${SWAP_FREE} free,\s+${SWAP_AVAIL} Avail Mem
 ^\s*PID\s*USER\s*PR\s*NI\s*VIRT\s*RES\s*SHR\s*S\s*.CPU\s*.MEM\s*TIME.\s*COMMAND -> Next
 ^\s*${PID}\s+${USER}\s+${PR}\s+${NI}\s+${VIRT}\s+${RES}\s+${SHR}\s+${S}\s+${CPU}\s+${MEM}\s+${TIME}\s+${COMMAND} -> Record
 ^\s*$$
 ^. -> Error
 