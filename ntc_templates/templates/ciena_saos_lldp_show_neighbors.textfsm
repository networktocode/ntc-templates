Value List,Filldown REMOTE_ADDR_IPV4 (\d+\.\d+\.\d+\.\d+)
Value List,Filldown REMOTE_ADDR_IPV6 ([a-f0-9:]+)
Value Filldown SYSTEM_NAME (\S+)
Value Filldown SYSTEM_DESCRIPTION (.*\S)
Value Required LOCAL_PORT (\S+)
Value REMOTE_PORT (\S+)
Value REMOTE_CHASSIS_ID (\S+)
Value List REMOTE_MGMT_ADDR_IPV4 (\d+\.\d+\.\d+\.\d+)
Value List REMOTE_MGMT_ADDR_IPV6 ([a-f0-9:]+)
Value REMOTE_SYSTEM_NAME (\S+)
Value REMOTE_SYSTEM_DESCRIPTION (.*\S)
Value REMOTE_SYNCE_SUPPORT (.*\S)
Value REMOTE_SYNCE_CONFIG (.*\S)

Start
  ^\+\-+
  ^\|\sRemote\sAddr:\s${REMOTE_ADDR_IPV4}
  ^\|\sRemote\sAddr:\s${REMOTE_ADDR_IPV6}
  ^\|\sSystem\sName:\s${SYSTEM_NAME}\s+\|
  ^\|\sSystem\sDesc:\s${SYSTEM_DESCRIPTION}\s+\|
  # neighbor record
  ^\|${LOCAL_PORT}\s*\|${REMOTE_PORT}\s*\|\s*Chassis\sId:\s*${REMOTE_CHASSIS_ID} -> Neighbor
  ^\|.*
  ^\s*$$
  ^. -> Error

Neighbor
  ^\|\s*\|\s*\|\s*Mgmt\sAddr:\s${REMOTE_MGMT_ADDR_IPV4}\s+\| -> NeighborMgmt
  ^\|\s*\|\s*\|\s*Mgmt\sAddr:\s${REMOTE_MGMT_ADDR_IPV6}\s+\| -> NeighborMgmt
  ^\|\s*\|\s*\|\s*System\sName:\s${REMOTE_SYSTEM_NAME} -> Neighbor
  ^\|\s*\|\s*\|\s*System\sDesc:\s${REMOTE_SYSTEM_DESCRIPTION}\s+\|
  ^\|\s*\|\s*\|\s*SyncE\sSuppt:\s${REMOTE_SYNCE_SUPPORT}\s+\|
  ^\|\s*\|\s*\|\s*SyncE\sConfg:\s${REMOTE_SYNCE_CONFIG}\s+\| 
  ^\+\-+ -> Record Start

NeighborMgmt
  ^\|\s*\|\s*\|\s+${REMOTE_MGMT_ADDR_IPV4}\s+\|$$
  ^\|\s*\|\s*\|\s+${REMOTE_MGMT_ADDR_IPV6}\s+\|$$
  ^\|\s*\|\s*\|\s*System\sName:\s${REMOTE_SYSTEM_NAME} -> Neighbor
