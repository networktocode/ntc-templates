Value DESCRIPTION (.+)
Value DRIVER_NAME (.+)
Value BOARD (\w+)
Value LIF_ID (\d+)
Value LIF_OID (\d+)
Value NETDEV_OID (\d+)
Value CURRENT_HWADDR ([a-zA-Z0-9]{2}(:[a-zA-Z0-9]{2}){5})
Value PERMANENT_HWADDR ([a-zA-Z0-9]{2}(:[a-zA-Z0-9]{2}){5})
Value ADMIN (\S+)
Value NETDEV_STATUS (\S+)
Value AUTONEGO_SETTING (\d+)
Value LINK_SETTING (\d+)
Value SPEED_SETTING (\d+)
Value DUPLEX_SETTING (\d+)
Value SPEED (\d+)
Value DUPLEX (\S+)
Value LINK_STATUS (\S+)
Value RX_PKTS (\d+)
Value RX_BYTES (\d+)
Value TX_PKTS (\d+)
Value TX_BYTES (\d+)
Value HOST_RX_PKTS (\d+)
Value HOST_RX_BYTES (\d+)
Value HOST_TX_PKTS (\d+)
Value HOST_TX_BYTES (\d+)
Value HOST_TX_DROPPED (\d+)
Value FRAGTXCREATE (\d+)
Value FRAGTXOK (\d+)
Value FRAGTXDROP (\d+)
Value List MEMBER_PORTS (\[\d+\]:\s+\S+)

Start
  ^Description\s*:${DESCRIPTION}\s*$$
  ^Driver\s+Name\s*:${DRIVER_NAME}\s*$$
  ^Board\s*:${BOARD}\s*$$
  ^lif\s+id\s*:${LIF_ID}\s*$$
  ^lif\s+oid\s*:${LIF_OID}\s*$$
  ^netdev\s+oid\s*:${NETDEV_OID}\s*$$
  ^Current_HWaddr\s*${CURRENT_HWADDR}\s*$$
  ^Permanent_HWaddr\s*${PERMANENT_HWADDR}\s*$$
  ^=+\s+Link\s+Status\s+=+ -> LinkStatus
  ^=+\s+Counters\s+=+ -> Counters
  ^\s*$$
  ^. -> Error

LinkStatus
  ^Admin\s*:${ADMIN}\s*$$
  ^netdev\s+status\s*:${NETDEV_STATUS}\s*$$
  ^autonego_setting\s*:${AUTONEGO_SETTING}\s*$$
  ^link_setting\s*:${LINK_SETTING}\s*$$
  ^speed_setting\s*:${SPEED_SETTING}\s*$$
  ^duplex_setting\s*:${DUPLEX_SETTING}\s*$$
  ^Speed\s*:${SPEED}\s*$$
  ^Duplex\s*:${DUPLEX}\s*$$
  ^link_status\s*:${LINK_STATUS}\s*$$ -> Start
  ^\s*$$
  ^. -> Error

Counters
  ^Rx\s+Pkts\s*:${RX_PKTS}\s*$$
  ^Rx\s+Bytes\s*:${RX_BYTES}\s*$$
  ^Tx\s+Pkts\s*:${TX_PKTS}\s*$$
  ^Tx\s+Bytes\s*:${TX_BYTES}\s*$$
  ^Host\s+Rx\s+Pkts\s*:${HOST_RX_PKTS}\s*$$
  ^Host\s+Rx\s+Bytes\s*:${HOST_RX_BYTES}\s*$$
  ^Host\s+Tx\s+Pkts\s*:${HOST_TX_PKTS}\s*$$
  ^Host\s+Tx\s+Bytes\s*:${HOST_TX_BYTES}\s*$$
  ^Host\s+Tx\s+dropped\s*:${HOST_TX_DROPPED}\s*$$
  ^FragTxCreate\s*:${FRAGTXCREATE}\s*$$
  ^FragTxOk\s*:${FRAGTXOK}\s*$$
  ^FragTxDrop\s*:${FRAGTXDROP}\s*$$
  # No 'Member Ports' parsing
  ^Member\s+Ports\s*\:\s*$$
  ^\s*${MEMBER_PORTS}\s*$$
  ^\s*$$
  ^. -> Error
